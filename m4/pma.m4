dnl Check for PMA and dependencies
dnl Copyright (C) 2004, 2005, 2021 Free Software Foundation, Inc.
dnl
dnl This file is free software, distributed under the terms of the GNU
dnl General Public License.  As a special exception to the GNU General
dnl Public License, this file may be distributed as part of a program
dnl that contains a configuration script generated by Autoconf, under
dnl the same distribution terms as the rest of that program.
dnl
dnl Defines HAVE_PMA to 1 if a working PMA setup is
dnl found, and sets @LIBPMA@ to the necessary library.

AC_DEFUN([GAWK_CHECK_PMA],
[
  AC_ARG_WITH([persistent-malloc],
     AS_HELP_STRING([--with-persistent-malloc=DIR],
	[look for the persistent memory allocator (pma) library in DIR]),
     [_do_pma=$withval],[_do_pma=no])

  if test "$_do_pma" != "no" ; then
     if test -d "$withval" ; then
        CPPFLAGS="${CPPFLAGS} -I$withval/include"
        LDFLAGS="${LDFLAGS} -L$withval/lib -no-pie"
     fi

        _pma_save_libs=$LIBS
        _combo="-lpma"
        LIBS="$LIBS $_combo"

        AC_MSG_CHECKING([whether pma_init via "$_combo" is present and usable])

        AC_LINK_IFELSE([
	AC_LANG_PROGRAM([
#include <stdio.h>
#include <pma.h>
],[
pma_init(1, "");
])],_found_pma=yes,_found_pma=no)

        AC_MSG_RESULT([$_found_pma])

        LIBS=$_pma_save_libs

        if test $_found_pma = yes ; then
           AC_DEFINE(HAVE_PMA,1,
	      [Define to 1 if you have fully functional pma library.])
           AC_DEFINE(SUPPORT_PERSIST, 1, [enable persistence support])
           AC_SUBST(LIBPMA,$_combo)
           break
        else 
           AC_MSG_ERROR(fully functional pma library not found)
        fi

     unset _pma_save_libs
     unset _combo
     unset _found_pma
  fi
])dnl
